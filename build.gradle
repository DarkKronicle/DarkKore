plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.0.1"
}

apply from: 'https://raw.githubusercontent.com/DarkKronicle/GradleScripts/main/gradle/publish.gradle'

version = "${project.mod_version}-${project.minecraft_version}"
group = project.maven_group

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroupByRegex 'io\\.github\\.onyxstudios.*'
        }
    }
    maven { url 'https://maven.terraformersmc.com/releases' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation "com.electronwill.night-config:toml:${project.night_config_version}"
    implementation "com.electronwill.night-config:json:${project.night_config_version}"

    implementation "com.github.DarkKronicle.Konstruct:addons:${project.konstruct_version}"
    implementation "com.github.DarkKronicle.Konstruct:core:${project.konstruct_version}"

    modImplementation ("io.github.ladysnake:satin:${satin_version}"){
        exclude group: "net.fabricmc.fabric-api"
    }

    include "io.github.ladysnake:satin:${satin_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    include "com.github.DarkKronicle.Konstruct:addons:${project.konstruct_version}"
    include "com.github.DarkKronicle.Konstruct:core:${project.konstruct_version}"

    include "com.electronwill.night-config:toml:${project.night_config_version}"
    include "com.electronwill.night-config:json:${project.night_config_version}"
    include "com.electronwill.night-config:core:${project.night_config_version}"

    modCompileOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
    }
}
